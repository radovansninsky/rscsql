plugins {
  id 'java'
  id 'maven-publish'
}

sourceCompatibility = 8

group "sk.rsc"
version "2.0.$rev"
buildTime = new Date().format('yyyy-MM-dd_HH-mm-ss')


repositories {
  mavenCentral()
}

dependencies {
  testImplementation group: 'org.testng', name: 'testng', version: '6.9.10'
  testImplementation group: 'com.h2database', name: 'h2', version: '1.4.184'
  testImplementation group: 'org.postgresql', name: 'postgresql', version: '42.1.4.jre7'
}

java {
  withSourcesJar()
  withJavadocJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(8)
  }
}

jar {
  manifest {
    attributes(
      'Implementation-Title': 'RSC Sql library',
      'Implementation-Version': version,
      'Implementation-Vendor': 'Radovan Sninsky',
      'Build-No': buildNo,
      'Build-Time': buildTime
    )
  }
}

javadoc {
  source = sourceSets.main.allJava
  options.overview = "src/main/java/overview.html"
}

publishing {
  publications {
    gpr(MavenPublication) {
      from components.java
    }
  }

  // telling gradle to publish artifact to repo
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/radovansninsky/rscsql")
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
        password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
      }
    }
  }
}
